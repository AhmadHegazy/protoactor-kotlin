apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task sourcesJar(type: Jar, dependsOn: project.classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

def pomConfig = {
    licenses {
        license {
            name "Apache License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "rogeralsing"
            name "Roger Alsing"
            email "rogeralsing@gmail.com"
        }
    }
    scm {
        url "https://github.com/AsynkronIT/protoactor-kotlin"
    }
}

project.extensions.create("artifact", ArtifactExtension)

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Proto.Actor is a Next generation Actor Model framework')
                root.appendNode('name', project.artifact.name)
                root.appendNode('url', 'http://proto.actor')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    dryRun = true
    publish = false

    user = findProperty("protoBintrayUser")
    key = findProperty("protoBintrayKey")

    publications = ['maven']
    pkg {
        repo = 'protoactor'
        userOrg = 'protoactor'
        name = project.name
        websiteUrl = "http://proto.actor/"
        vcsUrl = "https://github.com/AsynkronIT/protoactor-kotlin.git"
        licenses = ['Apache-2.0']
        labels = ['actor']
        version {
            gpg {
                sign = false //Determines whether to GPG sign the files. The default is false
                // passphrase = 'passphrase' //Optional. The passphrase for GPG signing'
            }
        }
    }
}

class ArtifactExtension {
    String name = 'Proto.Actor'
}
